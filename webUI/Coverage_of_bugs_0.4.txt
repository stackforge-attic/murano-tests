*** Settings ***
Suite Setup
Suite Teardown    Close All Browsers
Test Setup        Open Browser    ${url}    remote_url=http://172.18.124.233:4444/wd/hub    browser=firefox    ff_profile_dir=ffprofile
Test Teardown     Close All Browsers
Library           String
Library           Boffin.WebUIlib    0    5    # Boffin framework library

*** Variables ***
${resources_path}    Resources/
${url}            http://10.100.0.6/horizon


*** Test Cases ***
Create IIS+ ASP Farm + AD + IIS
    [Tags]    night
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "complex_env" for field Environment Name
    User click on "Create"
    User click on "complex_env"
    User click on "Create Service"
    User create IIS with name "iis1" and don't join to domain, select "Not in domain"
    User click on "Create Service"
    User create ASP.NET Farm with name "asp-farm" and don't join to domain, select "Not in domain"
    User click on "Create Service"
    User create Active Directory with domain name "singleAD.domain"
    User click on "Create Service"
    User create IIS with name "iis2" and don't join to domain, select "Not in domain"
    Delete environment "complex_env" after test

Create ASP.FARM + SQL Cluster + SQL + AD
    [Tags]    night
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "ASP.FARM + SQL Cluster + SQL + AD" for field Environment Name
    User click on "Create"
    User click on "ASP.FARM + SQL Cluster + SQL + AD"
    User click on "Create Service"
    User create ASP.NET Farm with name "asp-farm" and don't join to domain, select "Not in domain"
    User click on "Create Service"
    User create MS SQL cluster with name "sql-cluster" and join in domain "sqlAD.domain"
    User click on "Create Service"
    User create MS SQL server with name "sql-server" and join to domain, select "Not in domain"
    Delete environment "ASP.FARM + SQL Cluster + SQL + AD" after test

Create IIS + AD + SQL + SQL Cluster + ASP.FARM
    [Tags]    night
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "IIS + AD + SQL + SQL Cluster + ASP.FARM" for field Environment Name
    User click on "Create"
    User click on "IIS + AD + SQL + SQL Cluster + ASP.FARM"
    User click on "Create Service"
    User create IIS with name "iis" and don't join to domain, select "Not in domain"
    User click on "Create Service"
    User create Active Directory with domain name "singleAD.domain"
    User click on "Create Service"
    User create MS SQL server with name "mssql" and don't join to domain, select "Not in domain"
    User click on "Create Service"
    User create MS SQL cluster with name "sql-cluster" and join in domain "sqlAD.domain"
    User click on "Create Service"
    User create ASP.NET Farm with name "asp-farm" and don't join to domain, select "Not in domain"
    Delete environment "IIS + AD + SQL + SQL Cluster + ASP.FARM" after test

Create 4 MS SQL clusters
    [Tags]    night
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "4 SQL Clusters" for field Environment Name
    User click on "Create"
    User click on "4 SQL Clusters"
    User click on "Create Service"
    User create MS SQL cluster with name "sql1-cluster" and join in domain "sql1.domain"
    User click on "Create Service"
    User create MS SQL cluster with name "sql2-cluster" and join in domain "sql2.domain"
    User click on "Create Service"
    User create MS SQL cluster with name "sql3-cluster" and join in domain "sql3.domain"
    User click on "Create Service"
    User create MS SQL cluster with name "sql4-cluster" and join in domain "sql4.domain"
    Delete environment "4 SQL Clusters" after test

Create IIS + SQL + ASP.FARM delete IIS
    [Tags]    night
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "IIS + SQL + ASP.FARM" for field Environment Name
    User click on "Create"
    User click on "IIS + SQL + ASP.FARM"
    User click on "Create Service"
    User create IIS with name "iis" and don't join to domain, select "Not in domain"
    User click on "Create Service"
    User create MS SQL server with name "sql-server" and don't join to domain, select "Not in domain"
    User click on "Create Service"
    User create ASP.NET Farm with name "asp-farm" and don't join to domain, select "Not in domain"
    Select "Delete Service" for chosen service "iis"
    User confirms deletion
    Page should not contain element "iis"
    Delete environment "IIS + SQL + ASP.FARM" after test

Add service in deployed env
    [Tags]    night
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "add_service_in_deployed_env" for field Environment Name
    User click on "Create"
    User click on "add_service_in_deployed_env"
    User click on "Create Service"
    User create IIS with name "iis" and don't join to domain, select "Not in domain"
    User click on "Deploy This Environment"
    Check the status of environment "add_service_in_deployed_env" (should be "Ready")
    Check that deploy "add_service_in_deployed_env" finished
    Navigate To    Murano>Environments
    User click on "add_service_in_deployed_env"
    User click on "Create Service"
    User create IIS with name "iis2" and don't join to domain, select "Not in domain"
    Delete environment "add_service_in_deployed_env" after test

Check re-deploy
    [Tags]    night
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "re-deploy" for field Environment Name
    User click on "Create"
    User click on "re-deploy"
    User click on "Create Service"
    User create Active Directory with domain name "newdomain.com"
    User click on "Deploy This Environment"
    Check the status of environment "re-deploy" (should be "Ready")
    Check that deploy "re-deploy" finished
    Navigate To    Murano>Environments
    User click on "re-deploy"
    User click on "Create Service"
    User create IIS with name "iis2" and \ join in domain "newdomain.com"
    User click on "Deploy This Environment"
    Check the status of environment "re-deploy" (should be "Ready")
    Check that deploy "re-deploy" finished
    Delete environment "re-deploy" after test

Deploy instance with different flavor
    [Tags]    night
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "different_flavor" for field Environment Name
    User click on "Create"
    User click on "different_flavor"
    User click on "Create Service"
    User create IIS with name "iis-medium" with instance flavor m1.medium and select "Not in domain"
    User click on "Create Service"
    User select "Internet Information Services" from dropdown list "Service Type"
    User click on "Next"
    User set value "iis-large" for field Service Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User set value "iisLARGE" for field Hostname template
    User click on "Next"
    User select "m1.large" from dropdown list "Instance flavor"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"
    Page should contain element "iis-large"
    User click on "Deploy This Environment"
    Check the status of environment "different_flavor" (should be "Ready")
    Check that deploy "different_flavor" finished
    Navigate To    Project>Instances
    Check that size of instance "iis" is "m1.medium"
    Check that size of instance "iisLARGE" is "m1.large"
    Navigate To    Project>Environments
    Delete environment "different_flavor" after test

Linux and Windows services in one environment
    [Tags]    night
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "ad_and_telnet" for field Environment Name
    User click on "Create"
    User click on "ad_and_telnet"
    User click on "Create Service"
    User create Active Directory with domain name "windows.service"
    User click on "Create Service"
    User select "Linux Telnet" from dropdown list "Service Type"
    User click on "Next"
    User set value "telnet-service" for field Service Name
    User set value "telnet" for field Hostname
    User click on "Next"
    User select "Linux Image" from dropdown list "Instance image"
    User click on "Create"
    User click on "Deploy This Environment"
    Check the status of environment "ad_and_telnet" (should be "Ready")
    Check that deploy "ad_and_telnet" finished

Checking type and last operation
    [Tags]    night
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "test" for field Environment Name
    User click on "Create"
    User click on "test"
    User click on "Create Service"
    User create IIS with name "service-iis" and don't join to domain, select "Not in domain"
    User click on "Deploy This Environment"
    Check that Type of service "service-iis" is "Internet Information Services"
    Check that Last operation of service "service-iis" is "Service draft"
    Check the status of environment "test" (should be "Ready")
    Check that deploy "test" finished
    Delete environment "test" after test

*** Keywords ***
Log in WebUI by ${user}/${password}
    Fill Field    User Name    ${user}
    Fill Field    Password    ${password}
    Click on    Sign In
    Navigate to    Murano>Environments

User set value "${value}" for field ${field}
    Fill Field    ${field}    ${value}

Check the status of environment "${name}" (should be "${status}")
    Navigate to    Project>Environments
    Wait Until Keyword Succeeds    40 min    5s    Check that Status of environment "${name}" is "${status}"

User click on "${element}"
    Click on    ${element}

Page should contain element "${element}"
    Page Should Contain    ${element}

Page should not contain element "${element}"
    Reload Page
    Page Should Not Contain    ${element}

User select "${item}" from dropdown list "${menu}"
    Select Item From List    ${menu}    ${item}

User confirms deletion
    Click On    Confirm deletion

User create Active Directory with domain name "${name}"
    User select "Active Directory" from dropdown list "Service Type"
    User click on "Next"
    User set value "${name}" for field Domain Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User set value "P@ssw0rd" for field Recovery password
    User set value "P@ssw0rd" for field Confirm recovery password
    User set value "${name}" for field Hostname template
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"

Select "${action}" for chosen ${type} "${env}"
    ${xpath}=    Find Associated Element    ${env}    ${action}
    Click Element    ${xpath}

Check that deploy "${env}" finished
    Navigate To    Project>Environments
    Select "More" for chosen environment "${env}"
    Select "Show Deployments" for chosen environment "${env}"
    Reload Page
    ${text}=    Get Text    xpath=/html/body/div/div[2]/div[3]/form/table/tbody/tr/td[3]
    Should Be Equal As Strings    ${text}    Successful

User create IIS with name "${name}" ${join} "${domain}"
    User select "Internet Information Services" from dropdown list "Service Type"
    User click on "Next"
    User set value "${name}" for field Service Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User select "${domain}" from dropdown list "Domain"
    User set value "iis" for field Hostname template
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"
    Page Should Contain    ${name}

User create ASP.NET App with name "${name}" ${join} "${domain}"
    User select "ASP.NET Application" from dropdown list "Service Type"
    User click on "Next"
    User set value "${name}" for field Service Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User select "${domain}" from dropdown list "Domain"
    User set value "git://github.com/Mirantis/murano-mvc-demo.git" for field Git repository
    User set value "asp" for field Hostname template
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"
    Page should contain element "${name}"

User create IIS Farm with name "${name}" ${join} "${domain}"
    User select "Internet Information Services Web Farm" from dropdown list "Service Type"
    User click on "Next"
    User set value "${name}" for field Service Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User select "${domain}" from dropdown list "Domain"
    User set value "iisfarm#" for field Hostname template
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"
    Page should contain element "${name}"

User create ASP.NET Farm with name "${name}" ${join} "${domain}"
    User select "ASP.NET Application Web Farm" from dropdown list "Service Type"
    User click on "Next"
    User set value "${name}" for field Service Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User select "${domain}" from dropdown list "Domain"
    User set value "git://github.com/Mirantis/murano-mvc-demo.git" for field Git repository
    User set value "aspfarm#" for field Hostname template
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"
    Page should contain element "${name}"

User create MS SQL server with name "${name}" ${join} "${domain}"
    User select "MS SQL Server" from dropdown list "Service Type"
    User click on "Next"
    User set value "${name}" for field Service Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User select "${domain}" from dropdown list "Domain"
    User set value "P@ssw0rd" for field SA Password
    User set value "P@ssw0rd" for field Confirm SA password SQL
    User set value "sql" for field Hostname template
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"
    Page should contain element "${name}"

User create MS SQL cluster with name "${name}" ${join} "${domain}"
    User create Active Directory with domain name "${domain}"
    Page should contain element "${domain}"
    User click on "Create Service"
    User select "MS SQL Server Cluster" from dropdown list "Service Type"
    User click on "Next"
    User set value "${name}" for field Service Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User select "${domain}" from dropdown list "Domain"
    User set value "P@ssw0rd" for field SA Password
    User set value "P@ssw0rd" for field Confirm SA password
    User click on "Next"
    User set value "10.100.0.88" for field Cluster Static IP
    User set value "cluster" for field Cluster Name
    User set value "AG_name" for field Availability Group Name
    User set value "AG_listener_name" for field Availability Group Listener Name
    User set value "10.100.0.89" for field Availability Group Listener IP
    User set value "user" for field SQL User Name
    User set value "P@ssw0rd" for field SQL User Password
    User set value "P@ssw0rd" for field Confirm password
    User set value "sqlcluster#" for field Hostname template
    User click on "Next"
    User set value "testbase" for field Database list
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"
    Page should contain element "${name}"

Check that service "${service}" was joined in domain "${domain}"
    User click on "${service}"
    Page should contain element "${domain}"

Delete environment "${name}" after test
    Navigate To    Project>Environments
    Select "More" for chosen environment "${name}"
    Select "Delete Environment" for chosen environment "${name}"
    User confirms deletion
    Page should not contain element "${name}"

Check that ${subject} of ${element} "${name}" is "${status}"
    ${row}=    Get Table Row With    ${name}
    ${source}=    Get Source
    ${cell}=    Get Web Element Selector    ${subject}    ${source}    th    this
    ${#_of_column}=    Get Substring    ${cell}    -2    -1
    ${text}=    Get Text    xpath=${row}/td[${#_of_column}]
    Should Contain    ${text}    ${status}
