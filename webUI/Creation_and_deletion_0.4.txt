*** Settings ***
Suite Setup
Suite Teardown    Close All Browsers
Test Setup        Open Browser    ${url}    remote_url=http://172.18.124.233:4444/wd/hub    browser=firefox    ff_profile_dir=ffprofile
Test Teardown     Close All Browsers
Library           String
Library           Boffin.WebUIlib    0    20    # Boffin framework library

*** Variables ***
${resources_path}    Resources/
${url}            http://10.100.0.6/horizon

*** Test Cases ***
Create and Delete AD service
    [Tags]    commit    7
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "create_delete_ad" for field Environment Name
    User click on "Create"
    User click on "create_delete_ad"
    User click on "Create Service"
    User select "Active Directory" from dropdown list "Service Type"
    User click on "Next"
    User set value "muranotest.domain" for field Domain Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User set value "P@ssw0rd" for field Recovery password
    User set value "P@ssw0rd" for field Confirm recovery password
    User set value "muranotest.domain" for field Hostname template
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"
    User click on "Delete Service"
    User confirms deletion
    Page should not contain element "muranotest.domain"
    Delete environment "create_delete_ad" after test

Create and Delete IIS service
    [Tags]    commit    8
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "create_delete_iis" for field Environment Name
    User click on "Create"
    User click on "create_delete_iis"
    User click on "Create Service"
    User select "Internet Information Services" from dropdown list "Service Type"
    User click on "Next"
    User set value "iis_service" for field Service Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User set value "iis" for field Hostname template
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"
    User click on "Delete Service"
    User confirms deletion
    Page should not contain element "iis_service"
    Delete environment "create_delete_iis" after test

Create and Delete ASP.NET service
    [Tags]    commit    9
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "create_delete_asp" for field Environment Name
    User click on "Create"
    User click on "create_delete_asp"
    User click on "Create Service"
    User select "ASP.NET Application" from dropdown list "Service Type"
    User click on "Next"
    User set value "asp-service" for field Service Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User set value "git://github.com/Mirantis/murano-mvc-demo.git" for field Git repository
    User set value "asp" for field Hostname template
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"
    User click on "Delete Service"
    User confirms deletion
    Page should not contain element "asp-service"
    Delete environment "create_delete_asp" after test

Create and Delete IIS Farm service
    [Tags]    commit    10
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "create_delete_iis_farm" for field Environment Name
    User click on "Create"
    User click on "create_delete_iis_farm"
    User click on "Create Service"
    User select "Internet Information Services Web Farm" from dropdown list "Service Type"
    User click on "Next"
    User set value "iis-farm" for field Service Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User set value "10.0.1.70" for field Load Balancer VIP
    User set value "iisfarm#" for field Hostname template
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"
    User click on "Delete Service"
    User confirms deletion
    Page should not contain element "iis-farm"
    Delete environment "create_delete_iis_farm" after test

Create and Delete ASP.NET Farm service
    [Tags]    commit    11
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "create_delete_asp_farm" for field Environment Name
    User click on "Create"
    User click on "create_delete_asp_farm"
    User click on "Create Service"
    User select "ASP.NET Application Web Farm" from dropdown list "Service Type"
    User click on "Next"
    User set value "asp-farm" for field Service Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User set value "git://github.com/Mirantis/murano-mvc-demo.git" for field Git repository
    User set value "10.0.1.71" for field Load Balancer VIP
    User set value "aspfarm#" for field Hostname template
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"
    User click on "Delete Service"
    User confirms deletion
    Page should not contain element "asp-farm"
    Delete environment "create_delete_asp_farm" after test

Create and Delete MS SQL server
    [Tags]    commit    12
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "create_delete_mssql" for field Environment Name
    User click on "Create"
    User click on "create_delete_mssql"
    User click on "Create Service"
    User select "MS SQL Server" from dropdown list "Service Type"
    User click on "Next"
    User set value "ms_sql" for field Service Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User set value "P@ssw0rd" for field SA Password
    User set value "P@ssw0rd" for field Confirm SA password SQL
    User set value "sql" for field Hostname template
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"
    User click on "Delete Service"
    User confirms deletion
    Page should not contain element "ms_sql"
    Delete environment "create_delete_mssql" after test

Create and Delete MS SQL Cluster
    [Tags]    commit    13
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "create_delete_sqlcluster" for field Environment Name
    User click on "Create"
    User click on "create_delete_sqlcluster"
    User click on "Create Service"
    User create Active Directory with domain name "activeDirectory.mssql"
    Page should contain element "activeDirectory.mssql"
    User click on "Create Service"
    User select "MS SQL Server Cluster" from dropdown list "Service Type"
    User click on "Next"
    User set value "sql_cluster" for field Service Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User select "activeDirectory.mssql" from dropdown list "Domain"
    User set value "P@ssw0rd" for field SA Password
    User set value "P@ssw0rd" for field Confirm SA password
    User click on "Next"
    User set value "10.0.1.8" for field Cluster Static IP
    User set value "cluster" for field Cluster Name
    User set value "AG_name" for field Availability Group Name
    User set value "AG_listener_name" for field Availability Group Listener Name
    User set value "10.0.1.9" for field Availability Group Listener IP
    User set value "user" for field SQL User Name
    User set value "P@ssw0rd" for field SQL User Password
    User set value "P@ssw0rd" for field Confirm password
    User set value "sqlcluster#" for field Hostname template
    User click on "Next"
    User set value "testbase" for field Database list
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"
    Select "Delete Service" for chosen service "sql_cluster"
    User confirms deletion
    Page should not contain element "sql_cluster"
    Delete environment "create_delete_sqlcluster" after test

Create and Delete Telnet Service
    [Tags]    commit    14
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "create_delete_telnet" for field Environment Name
    User click on "Create"
    User click on "create_delete_telnet"
    User click on "Create Service"
    User select "Linux Telnet" from dropdown list "Service Type"
    User click on "Next"
    User set value "telnet-service" for field Service Name
    User select "murano-lb-key" from dropdown list "Key Pair"
    User set value "telnet" for field Hostname
    User click on "Next"
    User select "Linux Image" from dropdown list "Instance image"
    User click on "Create"
    User click on "Delete Service"
    User confirms deletion
    Page should not contain element "telnet-service"
    Delete environment "create_delete_telnet" after test

Create and Delete Apache service
    [Tags]    commit    15
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "create_delete_apache" for field Environment Name
    User click on "Create"
    User click on "create_delete_apache"
    User click on "Create Service"
    User select "Linux Apache" from dropdown list "Service Type"
    User click on "Next"
    User set value "apache-service" for field Service Name
    User set value "apache" for field Hostname
    User click on "Next"
    User select "Linux Image" from dropdown list "Instance image"
    User click on "Create"
    User click on "Delete Service"
    User confirms deletion
    Page should not contain element "apache-service"
    Delete environment "create_delete_apache" after test

Check opportunity to choose Instance Flavor
    [Tags]    commit    16
    Log in WebUI by AutotestUser/swordfish
    User click on "Create Environment"
    User set value "env_inst_flavor" for field Environment Name
    User click on "Create"
    User click on "env_inst_flavor"
    User click on "Create Service"
    User select "Internet Information Services" from dropdown list "Service Type"
    User click on "Next"
    User set value "iis-service" for field Service Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User set value "iis" for field Hostname template
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User select "m1.small" from dropdown list "Instance flavor"
    User select "m1.large" from dropdown list "Instance flavor"
    User select "m1.medium" from dropdown list "Instance flavor"
    Delete environment "env_inst_flavor" after test

*** Keywords ***
Log in WebUI by ${user}/${password}
    Fill Field    User Name    ${user}
    Fill Field    Password    ${password}
    Click on    Sign In
    Navigate to    Murano>Environments

User set value "${value}" for field ${field}
    Fill Field    ${field}    ${value}

Check the status of environment "${name}" (should be "${status}")
    Navigate to    Murano>Environments
    Wait Until Keyword Succeeds    40 min    5s    Check that Status of environment "${name}" is "${status}"

User click on "${element}"
    Click on    ${element}

Page should contain element "${element}"
    Page Should Contain    ${element}

Page should not contain element "${element}"
    Reload Page
    Page Should Not Contain    ${element}

User select "${item}" from dropdown list "${menu}"
    Select Item From List    ${menu}    ${item}

User confirms deletion
    Click On    Confirm deletion

User create Active Directory with domain name "${name}"
    User select "Active Directory" from dropdown list "Service Type"
    User click on "Next"
    User set value "${name}" for field Domain Name
    User set value "P@ssw0rd" for field Administrator password
    User set value "P@ssw0rd" for field Confirm password
    User set value "P@ssw0rd" for field Recovery password
    User set value "P@ssw0rd" for field Confirm recovery password
    User set value "${name}" for field Hostname template
    User click on "Next"
    User select "Windows Server 2012 Standard" from dropdown list "Instance image"
    User click on "Create"

Select "${action}" for chosen ${type} "${env}"
    ${xpath}=    Find Associated Element    ${env}    ${action}
    Click Element    ${xpath}

Delete environment "${name}" after test
    Navigate To    Murano>Environments
    Select "More" for chosen environment "${name}"
    Select "Delete Environment" for chosen environment "${name}"
    User confirms deletion
    Page should not contain element "${name}"
