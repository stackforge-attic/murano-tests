*** Settings ***
Suite Setup
Suite Teardown    Close All Browsers
Test Setup        Open Browser    ${url}    remote_url=http://172.18.124.233:4444/wd/hub    browser=firefox    ff_profile_dir=ffprofile
Test Teardown     Close All Browsers
Library           String
Library           Boffin.WebUIlib    0    20    # Boffin framework library
Resource          Resources/objrepo/keywords.txt

*** Variables ***
${resources_path}    Resources/
${url}            http://10.100.0.58/horizon
${files_path}     /Files_for_autotests/

*** Test Cases ***
Check opportunity to compose a new service in repository
    [Tags]    night    commit
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    User click on "Compose Service"
    User set value "New Test Service" for field Service_Name
    User set value "newService" for field Fully Qualified Service Name
    User set value "<strong> New Test Service </strong> Here is a new description" for field Description
    User click on "UI Files"
    Choose Demo.yaml (set checkbox on in the 3 column)
    User click on "Workflows"
    Choose Demo.xml (set checkbox on in the 3 column)
    User click on "Heat Templates"
    Choose Demo.template (set checkbox on in the 3 column)
    User click on "Scripts"
    Choose CopyPrerequisites.ps1 (set checkbox on in the 3 column)
    User click on "Submit"
    Page should contain element "New Test Service"

Check opportunity to toggle service (make it active/inactive)
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    Select "More" for chosen service "Internet Information Services"
    Select "Toggle Active" for chosen service "Internet Information Services"
    Check that Active of element "Internet Information Services" is "False"
    Select "More" for chosen service "Internet Information Services"
    Select "Toggle Active" for chosen service "Internet Information Services"
    Check that Active of element "Internet Information Services" is "True"

Modify Service(change name)
    [Tags]    night    commit
    Log in WebUI by WebTestUser/swordfish
    User click on "Create Environment"
    User set value "test" for field Environment Name
    User click on "Create"
    Navigate to    Murano>Service Definitions
    Select "Modify Service" for chosen service "Linux Apache"
    User set value "Linux Apache2" for field Service_Name
    User click on "Submit"
    Navigate to    Murano>Environments
    User click on "test"
    User click on "Create Service"
    User select "Linux Apache2" from dropdown list "Service Type"
    User click on "Next"
    Page should contain element "Linux Apache2"

Modify Service (change description)
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    Select "Modify Service" for chosen service "Active Directory"
    User set value "<strong> Active Directory </strong> Here is a new description" for field Description
    User click on "Submit"
    Select "More" for chosen service "Active Directory"
    Select "Manage Service" for chosen service "Active Directory"
    Page should contain element "Description"
    Page should contain element "<strong> Active Directory </strong> Here is a new description"

Check opportunity to select composed service in Service Type list
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    Compose a new service "Test service"
    Navigate To    Murano>Environments
    User click on "test"
    User click on "Create Service"
    User select "Test service" from dropdown list "Service Type"
    User click on "Next"
    Page should contain element "Service Name"

Check opportunity to delete service from repository
    [Tags]    night    commit
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    Compose a new service "Service for deletion"
    Select "More" for chosen service "Service for deletion"
    Select "Delete Service" for chosen service "Service for deletion"
    User confirms deletion
    Page should not contain element "Service for deletion"

Check opportunity to toggle files (make few services active/inactive)
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    Choose Internet Information Services (set checkbox on in the 1 column)
    Choose Active Directory (set checkbox on in the 1 column)
    User click on "Toggle Active"
    Check that Active of element "Internet Information Services" is "False"
    Check that Active of element "Active Directory" is "False"
    Choose Internet Information Services (set checkbox on in the 1 column)
    Choose Active Directory (set checkbox on in the 1 column)
    User click on "Toggle Active"
    Check that Active of element "Internet Information Services" is "True"
    Check that Active of element "Active Directory" is "True"

Check opportunity to download service in repository
    [Tags]    don'twork
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    User click on "Upload Service"
    Choose and download file "myService.tar.gz" to repository
    Page should contain element "My Service"

Check opportunity to manage service (add files to existing service)
    [Tags]    don'twork
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    Compose a new service "Service"
    Select "More" for chosen service "Service"
    Select "Manage Service" for chosen service "Service"
    User click on "Heat Templates"
    Choose and download file "myHeatTemplate.template" to repository
    Page should contain element "myHeatTemplate.template"
    User click on "Scripts"
    Choose and download file "myScript.ps1" to repository
    Page should contain element "myScript.ps1"

Check opportunity to manage service (delete component from existing service)
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    Select "More" for chosen service "Service"
    Select "Manage Service" for chosen service "Service"
    Select "More" for chosen file "Demo.template"
    Select "Delete File" for chosen file "Demo.template"
    User confirms deletion
    Page should not contain element "Demo.template"

Manage Files (Check ability to add file to repository)
    [Tags]    don'twork
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    User click on "Manage Files"
    User click on "Upload File"
    User select "UI Definition (*.yaml)" from dropdown list "File data type"
    Choose and download file "myYaml.yaml" to repository
    Page should contain element "myYaml.yaml"

Manage Files (Check ability to delete file from repository)
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    User click on "Manage Files"
    Choose Demo.yaml (set checkbox on in the 1 column)
    User click on "Delete Files"
    User confirms deletion
    Page should not contain element "Demo.yaml"
