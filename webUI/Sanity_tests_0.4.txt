*** Settings ***
Suite Setup
Suite Teardown    Close All Browsers
Test Setup        Open Browser    ${url}    remote_url=http://172.18.124.233:4444/wd/hub    browser=firefox    ff_profile_dir=ffprofile
Test Teardown     Close All Browsers
Library           String
Library           Boffin.WebUIlib    0    20    # Boffin framework library

*** Variables ***
${resources_path}    Resources/
${url}            http://10.100.0.6/horizon

*** Test Cases ***
Create environment
    [Tags]    commit    1
    Log in WebUI by WebTestUser/swordfish
    User click on "Create Environment"
    User set value "env" for field Environment Name
    User click on "Create"
    Wait For Page Loaded
    Page should contain element "env"
    Delete environment "env" after test

Edit environment
    [Tags]    commit    2
    Log in WebUI by WebTestUser/swordfish
    User click on "Create Environment"
    User set value "env1" for field Environment Name
    User click on "Create"
    Select "More" for chosen environment "env1"
    Select "Edit Environment" for chosen environment "env1"
    User set value "edited_env" for field new Name
    User click on "Save"
    Page should contain element "edited_env"
    Delete environment "edited_env" after test

Delete Environment
    [Tags]    commit    3
    Log in WebUI by WebTestUser/swordfish
    User click on "Create Environment"
    User set value "env2" for field Environment Name
    User click on "Create"
    Select "More" for chosen environment "env2"
    Select "Delete Environment" for chosen environment "env2"
    User confirms deletion
    Page should not contain element "env2"

Delete Image from repo
    [Tags]    commit    4
    Log in WebUI by WebTestUser/swordfish
    User click on "Images"
    User click on "Mark Image"
    User select "ws-2012-std" from dropdown list "Image"
    User set value "Image for deletion" for field Title
    User select "Windows Server 2012" from dropdown list "with supported types of images"
    User click on "Mark"
    Page should contain element "Image for deletion"
    Select "Delete Metadata" for chosen image "Image for deletion"
    User confirms deletion
    Page should not contain element "Image for deletion"

Rename Windows image
    [Tags]    night    commit    5
    Log in WebUI by WebTestUser/swordfish
    User click on "Images"
    User click on "Mark Image"
    User select "ws-2012-std" from dropdown list "Image"
    User set value "Windows Server 2012 Standard" for field Title
    User select "Windows Server 2012" from dropdown list "with supported types of images"
    User click on "Mark"
    Page should contain element "Windows Server 2012 Standard"

Rename Linux image
    [Tags]    night    commit    5
    Log in WebUI by WebTestUser/swordfish
    User click on "Images"
    User click on "Mark Image"
    User select "cloud-centos" from dropdown list "Image"
    User set value "Linux Image" for field Title
    User select "Generic Linux" from dropdown list "with supported types of images"
    User click on "Mark"
    Page should contain element "Linux Image"

*** Keywords ***
Log in WebUI by ${user}/${password}
    Fill Field    User Name    ${user}
    Fill Field    Password    ${password}
    Click on    Sign In
    Navigate to    Murano>Environments

User set value "${value}" for field ${field}
    Fill Field    ${field}    ${value}

Select "${action}" for chosen ${type} "${env}"
    ${xpath}=    Find Associated Element    ${env}    ${action}
    Click Element    ${xpath}

User click on "${element}"
    Click on    ${element}

Page should contain element "${element}"
    Page Should Contain    ${element}

Page should not contain element "${element}"
    Reload Page
    Page Should Not Contain    ${element}

User select "${item}" from dropdown list "${menu}"
    Select Item From List    ${menu}    ${item}

User confirms deletion
    Click On    Confirm deletion

Delete environment "${name}" after test
    Navigate To    Murano>Environments
    Select "More" for chosen environment "${name}"
    Select "Delete Environment" for chosen environment "${name}"
    User confirms deletion
    Page should not contain element "${name}"
